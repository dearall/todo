plugins{
    id 'java-library'
}

apply from: 'databaseSetup.gradle'

dependencies {
    implementation project(':model')
    runtimeOnly 'com.h2database:h2:1.3.170'
    testImplementation 'junit:junit:4.13'
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.withType(Test) {
    testLogging {
        showStandardStreams = true
        exceptionFormat 'full'
    }
}

startDatabase.dependsOn test
integrationTest.dependsOn startAndPrepareDatabase
stopDatabase.dependsOn integrationTest
check.dependsOn stopDatabase

jar {
    dependsOn createBuildInfoFile
    from(buildDir) {
        include 'build-info.properties'
    }
}

// id 'maven-publish'
publishing {
    publications {
        repository(MavenPublication) {
            from components.java
            artifactId project.name

            pom {
                name = "Gradle CloudBees plugin"
                description = 'Gradle plugin for managing applications and databases on CloudBees RUN@cloud.'
                url = "https://github.com/dearall/todo.git"
                inceptionYear = '2021'
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = "johnd"
                        name = "John Doe"
                        email = "john.doe@example.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/dearall/todo.git"
                    developerConnection = "scm:git:https://github.com/dearall/todo.git"
                    url = "https://github.com/dearall/todo.git"
                }
            }

            artifact sourcesJar
            artifact javadocJar
        }
    }
}